group 'org.mskcc.kickoff.lims'
version '1.0-SNAPSHOT'

apply plugin: 'application'

mainClassName = "org.mskcc.kickoff.lims.CreateManifestSheet"

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'org.hamcrest', name: 'hamcrest-all', version: '1.3'
    compile group: 'org.apache.commons', name: 'commons-io', version: '1.3.2'
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.1'
    compile group: 'org.apache.poi', name: 'poi-ooxml', version: '3.9'
    compile group: 'javax.mail', name: 'mail', version: '1.4.1'
    compile group: 'org.springframework', name: 'spring-context', version: '4.3.7.RELEASE'
    compile group: 'org.springframework', name: 'spring-core', version: '4.3.7.RELEASE'
    compile group: 'log4j', name: 'log4j', version: '1.2.16'
    compile group: 'org.dbunit', name: 'dbunit', version: '2.4.3'
    compile group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.5'
    compile files('libs/cli-10.jar', 'libs/sapioutils.jar', 'libs/veloxapi.jar', 'libs/veloxclientutil.jar')

    compile group: 'junit', name: 'junit', version: '4.12'
    compile group: 'org.springframework', name: 'spring-test', version: '4.3.7.RELEASE'
}

task copyRuntimeLibs(type: Copy) {
//    delete fileTree(dir: "libs/")
    from configurations.testRuntime
    into "libs/"
}

tasks.withType(Test) {
    testLogging {
        events "standardOut", "started", "passed", "skipped", "failed"
    }
}

run {
    if (System.getProperty('DEBUG', 'false') == 'true') {
        jvmArgs '-Xdebug',
                '-Xrunjdwp:transport=dt_socket,server=y,suspend=y,address=5005'
    }

    if(project.hasProperty('programArgs')){
        args(programArgs.split(','))
    }
    systemProperties = System.properties
    environment SPRING_PROFILES_ACTIVE: environment.SPRING_PROFILES_ACTIVE ?: "prod,igo"
}

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integration-test/java')
        }
        resources.srcDir file('src/integration-test/resources')
        compileClasspath += sourceSets.main.runtimeClasspath + sourceSets.test.runtimeClasspath
        runtimeClasspath += sourceSets.main.runtimeClasspath + sourceSets.test.runtimeClasspath
    }
}

configurations {
    integrationTestCompile.extendsFrom compile
    integrationTestRuntime.extendsFrom compile
}

task integrationTest(type: Test) {
    filter {
        includeTestsMatching "org.mskcc.kickoff.characterisationTest.RegressionTest"
    }
    description = "Runs Integration Tests"
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath += sourceSets.integrationTest.runtimeClasspath
    systemProperties System.properties
    outputs.upToDateWhen { false }
}

